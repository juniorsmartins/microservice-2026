plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.github.davidmc24.gradle.plugin.avro" version "1.9.1"
}

group = 'backend.finance'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/" // Add para Avro/acessar dependências da Confluent
    }
}

ext {
    set('springCloudVersion', "2025.1.0")
}

dependencies {
    implementation project(':application-business-rules')

    implementation 'org.springframework.boot:spring-boot-starter-webmvc' // Para construir APIs RESTful
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test' // Dependências de teste para WebMVC

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa' // Para integração com JPA/Hibernate
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test' // Dependências de teste para JPA
    implementation 'org.springframework.boot:spring-boot-starter-liquibase' // Para versionamento e migração do banco de dados
    testImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test' // Dependências de teste para Liquibase
    implementation 'org.springframework.data:spring-data-envers:4.0.1' // Para auditoria de entidades JPA usando Envers
    runtimeOnly 'org.postgresql:postgresql' // Driver PostgreSQL
    runtimeOnly 'com.h2database:h2' // Banco de dados em memória para testes

    implementation 'org.springframework.boot:spring-boot-starter-kafka' // Para integração com Apache Kafka
    testImplementation 'org.springframework.boot:spring-boot-starter-kafka-test' // Para testes relacionados ao Kafka
    implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '8.0.0' //  Fornece a serialização e desserialização de mensagens Avro para o Kafka, permitindo enviar e receber dados codificados em Avro nos tópicos do Kafka.
    implementation group: 'io.confluent', name: 'kafka-schema-registry-client', version: '8.0.0' // Permite que sua aplicação interaja com o Schema Registry da Confluent, que gerencia os esquemas Avro, garantindo compatibilidade e evolução dos esquemas ao produzir ou consumir mensagens.
    testImplementation group: 'io.confluent', name: 'kafka-schema-registry', version: '8.0.0' // Necessário para mockar schema registry nos testes
    implementation group: 'org.apache.avro', name: 'avro', version: '1.12.0' // Utilizada para trabalhar com esquemas Avro e serializar/desserializar mensagens.

    compileOnly 'org.projectlombok:lombok' // Para reduzir código boilerplate (getters, setters, construtores, etc.)
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-actuator' // Para monitoramento e gerenciamento da aplicação
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test' // Dependências de teste para Actuator
    implementation 'org.springframework.cloud:spring-cloud-starter-config' // Para configuração centralizada com Spring Cloud Config
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client' // Para registro e descoberta de serviços com Eureka

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '6.0.0' // versão 6 compatível com Spring Boot 4
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

avro {
    createSetters = true // Gera métodos setters nos POJOs gerados a partir do Avro.
    fieldVisibility = "PRIVATE" // Define que os campos das classes geradas serão privados.
    outputCharacterEncoding = "UTF-8"
    enableDecimalLogicalType = true // suporte correto a BigDecimal (tipo logical decimal do Avro)
}

test {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = 'api-users.jar' // padronização do nome. Garante sempre ser api-users.jar, independente da versão.
}

jar {
    enabled = false
}

